% Input variables.
metavar vars ,  n , a, x , y, z , w , m , o ::=  
indexvar ivar, i , k , j , l ::=
metavar const, b ::= 

grammar


% Lambek category:
A, B, C :: 'lform_' ::=
  | Base    ::   :: Base          
  {{tex \mathsf{B} }}
  | Unit    ::   :: Unit
  {{tex \mathsf{Unit} }}
  | A (x) B ::   :: Tensor        
  | A -> B  ::   :: RFun          
  | A <- B  ::   :: LFun          
  | ( A )   :: M :: Parens 
  | h( A )  :: M :: HiddenParens 
  {{ tex [[A]] }}
  | F X     ::   :: LeftAdj
  {{tex \mathsf{F}[[X]] }}

% Symmetric monoidal closed category:
X, Y, Z :: 'sform_' ::=
  | Base    ::   :: Base          
  {{tex \mathsf{B} }}
  | Unit    ::   :: Unit
  {{tex \mathsf{Unit} }}
  | X (x) Y ::   :: Tensor        
  | X -o Y  ::   :: Fun          
  | ( X )   :: M :: Parens 
  | h( X )  :: M :: HiddenParens 
  {{ tex [[X]] }}
  | Gf A     ::   :: RightAdj
  {{tex \mathsf{G}[[A]] }}

T :: 'type_' ::=
  | A :: :: LType
  | X :: :: SType

p :: 'pattern_' ::=
  | - :: :: hole
  {{tex \star }}
  | x :: :: var
  | triv :: :: triv
  {{tex \mathsf{triv} }}
  | p (x) p' :: :: ten
  | F p :: :: LeftAdj
  {{tex \mathsf{F}\,[[p]] }}
  | Gf p     ::   :: RightAdj
  {{tex \mathsf{G}\,[[p]] }}

s :: 'term_' ::=
  | x :: :: Var
  | b :: :: Const
  | triv :: :: Unit
  {{tex \mathsf{triv} }}
  | let s1 : T be p in s2 :: :: let
  {{tex \mathsf{let}\,[[s1]] : [[T]]\,\mathsf{be}\,[[p]]\,\mathsf{in}\,[[s2]] }}
  | s1 (x) s2 :: :: Tensor
  | \l x : A . s :: :: LFun
  {{tex \lambda_l [[x]] : [[A]].[[s]] }}
  | \r x : A . s :: :: RFun
  {{tex \lambda_r [[x]] : [[A]].[[s]] }}
  | \ x : A . s :: :: Fun
  {{tex \lambda [[x]] : [[A]].[[s]] }}  
  | ( \l x : A . s1 ) s2 :: :: LFunApp
  {{tex (\lambda_l [[x]] : [[A]].[[s1]])\,[[s2]] }}
  | ( \r x : A . s1 ) s2 :: :: RFunApp
  {{tex (\lambda_r [[x]] : [[A]].[[s1]])\,[[s2]] }}
  | ( \ x : A . s1 ) s2 :: :: FunApp
  {{tex (\lambda [[x]] : [[A]].[[s1]])\,[[s2]] }}
  | appl s1 s2   :: :: Appl
  {{tex \mathsf{app}_l\,[[s1]]\,[[s2]] }}
  | appr s1 s2   :: :: Appr
  {{tex \mathsf{app}_r\,[[s1]]\,[[s2]] }}
  | app s1 s2   :: :: App
  {{tex \mathsf{app}\,[[s1]]\,[[s2]] }}  
  | ex x1 , x2 with s1 , s2 in s3 :: :: Ex
  {{tex \mathsf{ex}\,[[x1]],[[x2]]\,\mathsf{with}\,[[s1]],[[s2]]\,\mathsf{in}\,[[s3]] }}
  | contrR x1 as s1 , s2 in s3 :: :: ContrR
  {{tex \mathsf{contrR}\,[[x1]]\,\mathsf{as}\,[[s1]],[[s2]]\,\mathsf{in}\,[[s3]] }}
  | contrL x1 as s1 , s2 in s3 :: :: ContrL
  {{tex \mathsf{contrL}\,[[x1]]\,\mathsf{as}\,[[s1]],[[s2]]\,\mathsf{in}\,[[s3]] }}
  | weak x in s :: :: Weak
  {{tex \mathsf{weak}\,[[x]]\,\mathsf{in}\,[[s]] }}
  | derelict t :: :: Derelict
  {{tex \mathsf{derelict}\,[[t]] }}
  | [ s1 / x ] s2 :: M :: Subst
  | [ t / x ] s   :: M :: Subst2
  | ( s )         :: S :: parens
  | h( s )        :: M :: hparen
  {{tex [[s]] }}
  | F t           ::   :: LeftAdj
  {{tex \mathsf{F}[[t]] }}

t :: 'sterm_' ::=
  | x :: :: Var
  | b :: :: Const
  | triv :: :: Unit
  {{tex \mathsf{triv} }}
  | let t1 : X be p in t2 :: :: let
  {{tex \mathsf{let}\,[[t1]] : [[X]]\,\mathsf{be}\,[[p]]\,\mathsf{in}\,[[t2]] }}
  | t1 (x) t2 :: :: Tensor
  | \ x : X . t :: :: Fun
  {{tex \lambda [[x]] : [[X]].[[t]] }}  
  | app t1 t2   :: :: App
  {{tex \mathsf{app}\,[[t1]]\,[[t2]] }}  
  | ( \ x : X . t1 ) t2 :: :: FunApp
  {{tex (\lambda [[x]] : [[X]].[[t1]])\,[[t2]] }}
  | ex x1 , x2 with t1 , t2 in t3 :: :: Ex
  {{tex \mathsf{ex}\,[[x1]],[[x2]]\,\mathsf{with}\,[[t1]],[[t2]]\,\mathsf{in}\,[[t3]] }}
  | contrR x1 as t1 , t2 in t3 :: :: ContrR
  {{tex \mathsf{contrR}\,[[x1]]\,\mathsf{as}\,[[t1]],[[t2]]\,\mathsf{in}\,[[t3]] }}
  | contrL x1 as t1 , t2 in t3 :: :: ContrL
  {{tex \mathsf{contrL}\,[[x1]]\,\mathsf{as}\,[[t1]],[[t2]]\,\mathsf{in}\,[[t3]] }}
  | weak x in t :: :: Weak
  {{tex \mathsf{weak}\,[[x]]\,\mathsf{in}\,[[t]] }}  
  | [ t1 / x ] t2 :: M :: Subst
  | ( t )         :: S :: parens
  | h( t )        :: M :: hparen
  | Gf s           ::   :: RightAdj
  {{tex \mathsf{G}[[s]] }}

G {{tex \Gamma}}, D {{tex \Delta}}, I {{tex \Phi}}, P {{tex \Psi}} :: 'Ctx_' ::=
  | .       :: :: Empty
  {{tex \cdot }}
  | G1 , G2 :: :: Ext
  | x : A   :: :: El
  | ( G )   :: S :: Paren
  | x : X   ::   :: EParen

formula :: 'formula_' ::=
  | judgement            ::   :: judgement
  | formula1 && formula2 :: M :: quad
  {{tex [[formula1]] \quad [[formula2]] }}
  | formula1 ... formulai :: M :: vec
  | ( formula )          :: S :: parens
  {{tex [[formula]] }}
  | x nin FV ( s ) :: :: FV
  {{tex [[x]] \not\in \mathsf{FV}([[s]]) }}
  | x nin1 | G , D , P | :: :: NiNCtx1
  {{tex [[x]] \not\in | [[G]], [[D]], [[P]] | }}
  | x nin2 | G , D |     :: :: NiNCtx2
  {{tex [[x]] \not\in | [[G]], [[D]] | }}

terminals :: 'terminals_' ::=
  | Unit :: :: Unit
  {{tex \mathsf{Unit} }}
  | (x)  :: :: Tensor
  {{tex \otimes }}
  | (e)  :: :: ETensor
  {{tex \circop{e} }}  
  | (w)  :: :: WTensor
  {{tex \circop{w} }}  
  | (c)  :: :: CTensor
  {{tex \circop{c} }}
  | ->   :: :: LFun
  {{tex \rightharpoonup }}
  | <-   :: :: RFun
  {{tex \leftharpoonup }}
  | -o   :: :: Limp
  {{tex \multimap }}
  | |-   :: :: Vdash
  {{tex \vdash }}
  | ~>   :: :: To
  {{tex \leadsto }}
 
defns
  Jtype :: '' ::=

defn
  G |- t : X :: :: tty :: 'T_'
by 

  -------------- :: identity
  x : X |- x : X

  ---------------- :: unitI
  . |- triv : Unit

  D |- t1 : Unit && G |- t2 : Y
  --------------------------------------------- :: unitE
  G, D |- let t1 : Unit be triv in t : Y

  G |- t1 : X && D |- t2 : Y
  -------------------------- :: tenI
  G,D |- t1 (x) t2 : X (x) Y  

  G |- t1 : X (x) Y && D, x : X, y : Y |- t2 : Z
  ------------------------------------------------------ :: tenE
  G, D |- let t1 : X (x) Y be x (x) y in t2 : Z

  G, x : X |- t : Y
  ---------------------- :: impI
  G |- \ x:X.t : X -o Y
  
  G |- t1 : X -o Y && D |- t2 : X
  ----------------------------- :: impE
  G, D |- app t1 t2 : Y

  G ; . |- s : A
  ---------------- :: GI
  G |- Gf s : Gf A

defn
  G ; P |- s : A :: :: sty :: 'S_'
by

  ------------------ :: identity
  . ; x : A |- x : A

  -------------------- :: unitI
  . ; . |- triv : Unit

  D; I  |- s1 : Unit && G; P |- s2 : A
  --------------------------------------------- :: unitE
  G, D; P, I |- let s1 : Unit be triv in s2 : A

  G;P |- s1 : A && D;I |- s2 : B
  ------------------------------ :: tenI
  G,D;P,I |- s1 (x) s2 : A (x) B

  G |- z : X (x) Y && D, x : X, y : Y; P |- s : A
  --------------------------------------------------- :: tenE1
  D, G; P |- let z : X (x) Y be x (x) y in s : A

  G; P |- z : A (x) B && D; I, x : A, y : B |- s : C
  --------------------------------------------------- :: tenE2
  G, D; I, P |- let z : A (x) B be x (x) y in s : C

  G; P, x : A |- s : B
  ------------------------ :: implI
  G; P |- \l x:A.s : A -> B

  G; P |- s1 : A -> B && D; I |- s2 : A
  ------------------------------------ :: implE
  G, D; P, I |- appl s1 s2 : B

  G;x : A,P |- s : B
  ------------------------ :: imprI
  G;P |- \r x:A.s : B <- A

  G; P |- s1 : B <- A && D; I |- s2 : A
  ------------------------------------ :: imprE
  G, D; P, I |- appr s1 s2 : B

  G |- t : X
  ---------------- :: FI
  G;. |- F t : F X

  G ; P |- y : F X && G , x : X ; I |- s2 : A
  ------------------------------------------ :: FE
  G ; P , I |- let F x : F X be y in s2 : A

  G |- t : Gf A
  ----------------------- :: GE
  G ; . |- derelict t : A



defns
  Reduction :: '' ::=

defn
  t1 ~> t2 :: :: tred :: 'Tred_'
by 

  --------------------------------- :: letU
  let triv : Unit be triv in t ~> t

  ---------------------------------------------------------- :: letT
  let t1 (x) t2 : X (x) Y be x (x) y in t3 ~> [t1/x][t2/y]t3

  ------------------------- :: lam
  (\ x:X.t1) t2 ~> [t2/x]t1

  t1 ~> t1'
  ----------------------- :: app1
  app t1 t2 ~> app t1' t2

  t2 ~> t2'
  ----------------------- :: app2
  app t1 t2 ~> app t1 t2'



defn
  s1 ~> s2 :: :: sred :: 'Sred_'
by 

  --------------------------------- :: letU
  let triv : Unit be triv in s ~> s

  ---------------------------------------------------------- :: letT
  let s1 (x) s2 : A (x) B be x (x) y in s3 ~> [s1/x][s2/y]s3

  ----------------------------------- :: letF
  let F t : F X be F x in s ~> [t/x]s

  ------------------------- :: lamL
  (\l x:A.s1) s2 ~> [s2/x]s1

  ------------------------- :: lamR
  (\r x:A.s1) s2 ~> [s2/x]s1

  ------------------------- :: lam
  (\ x:A.s1) s2 ~> [s2/x]s1
  
  s1 ~> s1'
  ------------------------- :: appl1
  appl s1 s2 ~> appl s1' s2

  s2 ~> s2'
  ------------------------- :: appl2
  appl s1 s2 ~> appl s1 s2'

  s1 ~> s1'
  ------------------------- :: appr1
  appr s1 s2 ~> appr s1' s2

  s2 ~> s2'
  ------------------------- :: appr2
  appr s1 s2 ~> appr s1 s2'

  s1 ~> s1'
  ------------------------- :: app1
  app s1 s2 ~> app s1' s2

  s2 ~> s2'
  ------------------------- :: app2
  app s1 s2 ~> app s1 s2'

  ------------------ :: derelict
  derelict Gf s ~> s

















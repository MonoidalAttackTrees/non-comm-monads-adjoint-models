% Input variables.
metavar vars ,  n , a, x , y, z , w , m , o ::=  
indexvar ivar, i , k , j , l ::=
metavar const, b ::= 

grammar


% Lambek category:
A, B, C :: 'lform_' ::=
  | Base    ::   :: Base          
  {{tex \mathsf{B} }}
  | UnitS   ::   :: UnitS
  {{tex \mathsf{UnitS} }}
  | A (>) B ::   :: Lambek
  | A -> B  ::   :: RFun          
  | A <- B  ::   :: LFun          
  | ( A )   :: M :: Parens 
  | h( A )  :: M :: HiddenParens 
  {{ tex [[A]] }}
  | F X     ::   :: LeftAdj
  {{tex \mathsf{F}[[X]] }}

% Symmetric monoidal closed category:
X, Y, Z :: 'sform_' ::=
  | Base    ::   :: Base          
  {{tex \mathsf{B} }}
  | UnitT   ::   :: UnitT
  {{tex \mathsf{UnitT} }}
  | X (x) Y ::   :: Tensor        
  | X -o Y  ::   :: Fun          
  | ( X )   :: M :: Parens 
  | h( X )  :: M :: HiddenParens 
  {{ tex [[X]] }}
  | Gf A     ::   :: RightAdj
  {{tex \mathsf{G}[[A]] }}

T :: 'type_' ::=
  | A :: :: LType
  | X :: :: SType

p :: 'pattern_' ::=
  | - :: :: hole
  {{tex \star }}
  | x :: :: var
  | trivT :: :: trivT
  {{tex \mathsf{trivT} }}
  | trivS :: :: trivS
  {{tex \mathsf{trivS} }}
  | p (x) p' :: :: ten
  | p (>) p' :: :: lambek
  | F p :: :: LeftAdj
  {{tex \mathsf{F}\,[[p]] }}
  | Gf p     ::   :: RightAdj
  {{tex \mathsf{G}\,[[p]] }}

s :: 'term_' ::=
  | x :: :: Var
  | b :: :: Const
  | trivS :: :: UnitS
  {{tex \mathsf{trivS} }}
  | let s1 : T be p in s2 :: :: let1
  {{tex \mathsf{let}\,[[s1]] : [[T]]\,\mathsf{be}\,[[p]]\,\mathsf{in}\,[[s2]] }}
  | let t : T be p in s :: :: let2
  {{tex \mathsf{let}\,[[t]] : [[T]]\,\mathsf{be}\,[[p]]\,\mathsf{in}\,[[s]] }}
  | s1 (>) s2 :: :: Tensor
  | \l x : A . s :: :: LFun
  {{tex \lambda_l [[x]] : [[A]].[[s]] }}
  | \r x : A . s :: :: RFun
  {{tex \lambda_r [[x]] : [[A]].[[s]] }}
  | appl s1 s2   :: :: Appl
  {{tex \mathsf{app}_l\,[[s1]]\,[[s2]] }}
  | appr s1 s2   :: :: Appr
  {{tex \mathsf{app}_r\,[[s1]]\,[[s2]] }}  
  | derelict t :: :: Derelict
  {{tex \mathsf{derelict}\,[[t]] }}
  | ex s1 , s2 with x1 , x2 in s3 :: :: Ex
  {{tex \mathsf{ex}\,[[s1]],[[s2]]\,\mathsf{with}\,[[x1]],[[x2]]\,\mathsf{in}\,[[s3]] }}
  | [ s1 / x ] s2 :: M :: Subst
  | [ t / x ] s   :: M :: Subst2
  | ( s )         :: S :: parens
  | h( s )        :: M :: hparen
  {{tex [[s]] }}
  | F t           ::   :: LeftAdj
  {{tex \mathsf{F}[[t]] }}

t :: 'sterm_' ::=
  | x :: :: Var
  | b :: :: Const
  | trivT :: :: UnitT
  {{tex \mathsf{trivT} }}
  | let t1 : X be p in t2 :: :: let
  {{tex \mathsf{let}\,[[t1]] : [[X]]\,\mathsf{be}\,[[p]]\,\mathsf{in}\,[[t2]] }}
  | t1 (x) t2 :: :: Tensor
  | \ x : X . t :: :: Fun
  {{tex \lambda [[x]] : [[X]].[[t]] }}  
  | app t1 t2   :: :: App
  {{tex \mathsf{app}\,[[t1]]\,[[t2]] }}  
  | ex t1 , t2 with x1 , x2 in t3 :: :: Ex
  {{tex \mathsf{ex}\,[[t1]],[[t2]]\,\mathsf{with}\,[[x1]],[[x2]]\,\mathsf{in}\,[[t3]] }}
  | [ t1 / x ] t2 :: M :: Subst
  | ( t )         :: S :: parens
  | h( t )        :: M :: hparen
  | Gf s           ::   :: RightAdj
  {{tex \mathsf{G}[[s]] }}

G {{tex \Gamma}}, D {{tex \Delta}}, I {{tex \Phi}}, P {{tex \Psi}} :: 'Ctx_' ::=
  | .       :: :: Empty
  {{tex \cdot }}
  | G1 , G2 :: :: Ext
  | x : A   :: :: El
  | ( G )   :: S :: Paren
  | x : X   ::   :: EParen

formula :: 'formula_' ::=
  | judgement            ::   :: judgement
  | formula1 && formula2 :: M :: quad
  {{tex [[formula1]] \quad [[formula2]] }}
  | formula1 ... formulai :: M :: vec
  | ( formula )          :: S :: parens
  {{tex [[formula]] }}
  | x nin FV ( s ) :: :: FV
  {{tex [[x]] \not\in \mathsf{FV}([[s]]) }}
  | x nin1 | G , D , P | :: :: NiNCtx1
  {{tex [[x]] \not\in | [[G]], [[D]], [[P]] | }}
  | x nin2 | G , D |     :: :: NiNCtx2
  {{tex [[x]] \not\in | [[G]], [[D]] | }}

terminals :: 'terminals_' ::=
  | Unit :: :: Unit
  {{tex \mathsf{Unit} }}
  | (x)  :: :: Tensor
  {{tex \otimes }}
  | (>)  :: :: Lambek
  {{tex \triangleright }}
  | (e)  :: :: ETensor
  {{tex \circop{e} }}  
  | (w)  :: :: WTensor
  {{tex \circop{w} }}  
  | (c)  :: :: CTensor
  {{tex \circop{c} }}
  | ->   :: :: LFun
  {{tex \rightharpoonup }}
  | <-   :: :: RFun
  {{tex \leftharpoonup }}
  | -o   :: :: Limp
  {{tex \multimap }}
  | |-   :: :: Vdash
  {{tex \vdash }}
  | ~>   :: :: To
  {{tex \leadsto }}
 
defns
  Jtype :: '' ::=

defn
  G |- t : X :: :: tty :: 'T_'
by 

  -------------- :: id
  x : X |- x : X

  ------------------ :: unitI
  . |- trivT : UnitT

  D |- t1 : UnitT && G |- t2 : Y
  --------------------------------------------- :: unitE
  G, D |- let t1 : UnitT be trivT in t2 : Y

  G |- t1 : X && D |- t2 : Y
  -------------------------- :: tenI
  G,D |- t1 (x) t2 : X (x) Y  

  G |- t1 : X (x) Y && D, x : X, y : Y |- t2 : Z
  ------------------------------------------------------ :: tenE
  G, D |- let t1 : X (x) Y be x (x) y in t2 : Z

  G, x : X |- t : Y
  ---------------------- :: impI
  G |- \ x:X.t : X -o Y
  
  G |- t1 : X -o Y && D |- t2 : X
  ----------------------------- :: impE
  G, D |- app t1 t2 : Y

  G ; . |- s : A
  ---------------- :: GI
  G |- Gf s : Gf A
  
  G, x : X, y : Y, D |- t : Z 
  ------------------------------------------------ :: beta
  G, z : Y, w : X, D |- ex w, z with x, y in t : Z

defn
  P |- s : A :: :: sty :: 'S_'
by

  --------------- :: id
  x : A |- x : A

  ------------------ :: unitI
  . |- trivS : UnitS

  P  |- s1 : UnitS && I |- s2 : A
  ---------------------------------------- :: unitE1
  P, I |- let s1 : UnitS be trivS in s2 : A

  P  |- s1 : UnitS && I |- s2 : A
  ---------------------------------------- :: unitE2
  I, P |- let s1 : UnitS be trivS in s2 : A

  G  |- t : UnitT && I |- s : A
  ---------------------------------------- :: unitE3
  G, I |- let t : UnitT be trivT in s : A

  G  |- t : UnitT && I |- s : A
  ---------------------------------------- :: unitE4
  I, G |- let t : UnitT be trivT in s : A

  I |- s1 : A && P |- s2 : B
  ------------------------------ :: tenI
  I, P |- s1 (>) s2 : A (>) B

  G |- t : X (x) Y && I, x : X, y : Y |- s : A
  -------------------------------------------- :: tenE1
  I, G |- let t : X (x) Y be x (x) y in s : A

  P |- s1 : A (>) B && I, x : A, y : B |- s2 : C
  ---------------------------------------------- :: tenE2
  I, P |- let s1 : A (>) B be x (>) y in s2 : C

  I, x : A |- s : B
  ---------------------- :: imprI
  I |- \r x:A.s : A -> B

  I |- s1 : A -> B && P |- s2 : A
  ------------------------------- :: imprE
  I, P |- appr s1 s2 : B

  x : A, x : A |- s : B
  ---------------------- :: implI
  I |- \l x:A.s : B <- A

  I |- s1 : B <- A && P |- s2 : A
  ------------------------------- :: implE
  I, P |- appl s1 s2 : B

  G |- t : X
  ---------------- :: FI
  G;. |- F t : F X

  P |- y : F X && I1, x : X, I2 |- s : A
  ------------------------------------------ :: FE
  I1, P, I2 |- let F x : F X be y in s : A

  G |- t : Gf A
  ----------------------- :: GE
  G ; . |- derelict t : A


  I, x : X, y : Y, P |- s : A
  ----------------------------------------------- :: beta
  I, z : Y, w : X, P |- ex w,z with x,y in s : A


defns
  Reduction :: '' ::=

defn
  t1 ~> t2 :: :: tred :: 'Tred_'
by 

  ------------------------------------ :: letU
  let trivT : UnitT be trivT in t ~> t

  ---------------------------------------------------------- :: letT
  let t1 (x) t2 : X (x) Y be x (x) y in t3 ~> [t1/x][t2/y]t3

  ------------------------- :: lam
  app (\ x:X.t1) t2 ~> [t2/x]t1

  t1 ~> t1'
  ----------------------- :: app1
  app t1 t2 ~> app t1' t2

  t2 ~> t2'
  ----------------------- :: app2
  app t1 t2 ~> app t1 t2'



defn
  s1 ~> s2 :: :: sred :: 'Sred_'
by 

  ------------------------------------ :: letU1
  let trivS : UnitS be trivS in s ~> s

  ------------------------------------ :: letU2
  let trivT : UnitT be trivT in s ~> s

  ---------------------------------------------------------- :: letT
  let s1 (>) s2 : A (>) B be x (>) y in s3 ~> [s1/x][s2/y]s3

  ----------------------------------- :: letF
  let F t : F X be F x in s ~> [t/x]s

  ------------------------------- :: lamL
  appl (\l x:A.s1) s2 ~> [s2/x]s1

  ------------------------------- :: lamR
  appr (\r x:A.s1) s2 ~> [s2/x]s1
  
  s1 ~> s1'
  ------------------------- :: appl1
  appl s1 s2 ~> appl s1' s2

  s2 ~> s2'
  ------------------------- :: appl2
  appl s1 s2 ~> appl s1 s2'

  s1 ~> s1'
  ------------------------- :: appr1
  appr s1 s2 ~> appr s1' s2

  s2 ~> s2'
  ------------------------- :: appr2
  appr s1 s2 ~> appr s1 s2'

  ------------------ :: derelict
  derelict Gf s ~> s

















% Input variables.
metavar vars ,  n , a, x , y, z , w , m , o ::=  
indexvar ivar, i , k , j , l ::=
metavar const, b ::= 

grammar


% Lambek category:
A, B, C, DT {{tex D }} :: 'lform_' ::=
  | Base    ::   :: Base          
  {{tex \mathsf{B} }}
  | UnitS    ::   :: UnitS
  {{tex \mathsf{Unit} }}
  | A (>) B ::   :: Tensor        
  | A -> B  ::   :: RFun          
  | A <- B  ::   :: LFun          
  | ( A )   :: M :: Parens 
  | h( A )  :: M :: HiddenParens 
  {{ tex [[A]] }}
  | F X     ::   :: LeftAdj
  {{tex \mathsf{F}[[X]] }}

% Symmetric monoidal closed category:
W, X, Y, Z :: 'sform_' ::=
  | Base    ::   :: Base          
  {{tex \mathsf{B} }}
  | UnitT    ::   :: UnitT
  {{tex \mathsf{Unit} }}
  | X (*) Y ::   :: Tensor        
  | X -o Y  ::   :: Fun          
  | ( X )   :: M :: Parens 
  | h( X )  :: M :: HiddenParens 
  {{ tex [[X]] }}
  | Gf A     ::   :: RightAdj
  {{tex \mathsf{G}[[A]] }}

T :: 'type_' ::=
  | A :: :: LType
  | X :: :: SType

I {{tex \Phi}}, P {{tex \Psi}} :: 'CCtx_' ::=
  | .       ::   :: Empty
  {{tex \cdot }}
  | I1 , I2 ::   :: Ext
  | X   ::   :: ElC
  | ( I )   :: S :: Paren

G {{tex \Gamma}}, D {{tex \Delta}} :: 'Ctx_' ::=
  | .       ::   :: Empty
  {{tex \cdot }}
  | A   ::   :: El
  | I       ::   :: CCtx
  | G1 ; G2 ::   :: Ext
  | ( G )   :: S :: Paren

formula :: 'formula_' ::=
  | judgement            ::   :: judgement
  | formula1 && formula2 :: M :: quad
  {{tex [[formula1]] \quad [[formula2]] }}
  | formula1 ... formulai :: M :: vec
  | ( formula )          :: S :: parens
  {{tex [[formula]] }}

terminals :: 'terminals_' ::=
  | (*)  :: :: Tensor
  {{tex \otimes }}
  | (>)  :: :: Lambek
  {{tex \triangleright }}
  | (e)  :: :: ETensor
  {{tex \circop{e} }}  
  | (w)  :: :: WTensor
  {{tex \circop{w} }}  
  | (c)  :: :: CTensor
  {{tex \circop{c} }}
  | ->   :: :: LFun
  {{tex \rightharpoonup }}
  | <-   :: :: RFun
  {{tex \leftharpoonup }}
  | -o   :: :: Limp
  {{tex \multimap }}
  | |-c   :: :: VdashC
  {{tex \vdash_\mathcal{C} }}
  | |-l   :: :: VdashL
  {{tex \vdash_\mathcal{L} }}
  | ~>   :: :: To
  {{tex \leadsto }}
 
defns
  Jtype :: '' ::=

defn
  I |-c X :: :: tty :: 'T_'
by 

  ------- :: ax
  X |-c X

  I, P |-c X
  ----------------- :: unitL
  I, UnitT, P |-c X

  ----------- :: unitR
  . |-c UnitT

  I, X, Y, P |-c Z
  ------------------- :: tenL
  I, X (*) Y, P |-c Z

  I |-c X && P |-c Y
  ------------------ :: tenR
  I, P |-c X (*) Y  

  I |-c X && P1, Y, P2 |-c Z
  -------------------------- :: impL
  P1, X -o Y, I, P2 |-c Z

  I, X, P |-c Y
  --------------- :: impR
  I, P |-c X -o Y

  I |-l A
  ---------- :: Gr
  I |-c Gf A

  I |-c X && P1, X, P2 |-c Y
  -------------------------- :: cut
  P1, I, P2 |-c Y

  I, X, Y, P |-c Z
  ---------------- :: ex
  I, Y, X, P |-c Z

defn
  G |-l A :: :: sty :: 'S_'
by

  ------- :: ax
  A |-l A

  G; D |-l A
  ----------------- :: unitL1
  G; UnitS; D |-l A

  G; D |-l A
  ----------------- :: unitL2
  G; UnitT; D |-l A

  ----------- :: unitR
  . |-l UnitS

  G; X; Y; D |-l A
  ---------------- :: ex
  G; Y; X; D |-l A

  G; X; Y; D |-l A
  ------------------- :: tenL1
  G; X (*) Y; D |-l A

  G; A; B; D |-l C
  ------------------- :: tenL2
  G; A (>) B; D |-l C

  G |-l A && D |-l B
  ------------------ :: tenR
  G; D |-l A (>) B

  I |-c X && G; Y; D |-l A
  ------------------------ :: impL
  G; X -o Y; I; D |-l A

  G |-l A && D1; B; D2 |-l C
  -------------------------- :: imprL
  D; A -> B; G; D2 |-l C

  G; A |-l B
  ------------ :: imprR
  G |-l A -> B

  G |-l A && D1; B; D2 |-l C
  -------------------------- :: implL
  D1; G; B <- A; D2 |-l C

  A; G |-l B
  ------------ :: implR
  G |-l B <- A

  G; X; D |-l A
  --------------- :: Fl
  G; F X; D |-l A

  I |-c X
  --------- :: Fr
  I |-l F X

  G; A; D |-l B
  ---------------- :: Gl
  G; Gf A; D |-l B

  I |-c X && D1; X; D2 |-l A
  -------------------------- :: cut1
  D1; I; D1 |-l A

  G |-l A && D1; A; D2 |-l B
  ---------------------------------------- :: cut2
  D1; G; D2 |-l B

